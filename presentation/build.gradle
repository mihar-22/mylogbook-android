apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    def config = rootProject.ext

    compileSdkVersion               config.androidCompileSdkVersion
    buildToolsVersion               config.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion               config.androidMinSdkVersion
        targetSdkVersion            config.androidTargetSdkVersion

        applicationId               config.androidApplicationId
        versionCode                 config.androidVersionCode
        versionName                 config.androidVersionName
        testInstrumentationRunner   config.testInstrumentationRunner
        testApplicationId           config.testApplicationId
    }

    compileOptions {
        sourceCompatibility         JavaVersion.VERSION_1_7
        targetCompatibility         JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            applicationIdSuffix     ".debug"
            buildConfigField        "String", "MYLB_HOST", "\"10.0.2.2\""
            buildConfigField        "String", "API_URL", "\"http://10.0.2.2:8000/api/v1/\""
        }

        release {
            minifyEnabled       false
            buildConfigField    "String", "MYLB_HOST", "\"mylb.com.au\""
            buildConfigField    "String", "API_URL", "\"https://mylb.com.au/api/v1/\""
            proguardFiles       getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events              "passed", "skipped", "failed", "standardOut"
            showExceptions      true
            exceptionFormat     "full"
            showCauses          true
            showStackTraces     true

            afterSuite { desc, result ->
                if (!desc.parent) {
                    def tests = "[${result.resultType}]: (${result.testCount} tests"
                    def passes = "${result.successfulTestCount} successes"
                    def fails = "${result.failedTestCount} failures"
                    def skips = "${result.skippedTestCount} skipped)"
                    def output = "${tests}, ${passes}, ${fails}, ${skips}"
                    def length = output.length()

                    println('\n' + ('-' * length) + '\n' + output + '\n' + ('-' * length))
                }
            }
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    def libraries = rootProject.ext.libraries

    compile (project(':domain')) { exclude module: 'joda-time' }
    compile project(':data')

    provided libraries.javaxAnnotation

    // Annotation Processors
    kapt libraries.daggerCompiler

    // General
    compile libraries.dagger
    compile libraries.rxJava
    compile libraries.rxAndroid
    compile libraries.stetho
    compile libraries.stethoOkHttp
    compile libraries.androidAppCompat
    compile libraries.androidAnnotations
    compile libraries.androidRecyclerView
    compile libraries.androidConstraintLayout
    compile libraries.androidDesign
    compile libraries.androidJoda

    // Kotlin
    compile libraries.kotlin
    compile libraries.rxKotlin
    compile libraries.rxBinding
    compile libraries.rxAppCompat
    compile libraries.rxDesign
    compile libraries.extrasDelegates

    // Debug
    compile libraries.leakCanary
    compile libraries.timber

    // Testing
    testCompile libraries.junit
    testCompile libraries.mockito
    testCompile libraries.kluent
    testCompile libraries.jodaTime

    // Android Testing
    androidTestCompile libraries.mockito
    androidTestCompile libraries.androidTestRunner
    androidTestCompile libraries.androidTestRules
    androidTestCompile libraries.androidEspresso
    androidTestCompile libraries.androidEspressoIntents
    androidTestCompile libraries.androidUiAutomator
    androidTestCompile libraries.androidAnnotations
}
