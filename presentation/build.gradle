apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    def config = rootProject.ext

    compileSdkVersion               config.androidCompileSdkVersion
    buildToolsVersion               config.androidBuildToolsVersion

    defaultConfig {
        minSdkVersion               config.androidMinSdkVersion
        targetSdkVersion            config.androidTargetSdkVersion

        applicationId               config.androidApplicationId
        versionCode                 config.androidVersionCode
        versionName                 config.androidVersionName
        testInstrumentationRunner   config.testInstrumentationRunner
        testApplicationId           config.testApplicationId

        multiDexEnabled             true
    }

    compileOptions {
        sourceCompatibility         JavaVersion.VERSION_1_8
        targetCompatibility         JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            applicationIdSuffix     ".debug"
            buildConfigField        "String", "MYLB_HOST", "\"10.0.2.2\""
            buildConfigField        "String", "API_URL", "\"http://10.0.2.2:8000/api/v1/\""
        }

        release {
            minifyEnabled       false
            buildConfigField    "String", "MYLB_HOST", "\"mylb.com.au\""
            buildConfigField    "String", "API_URL", "\"https://mylb.com.au/api/v1/\""
            proguardFiles       getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events              "passed", "skipped", "failed", "standardOut"
            showExceptions      true
            exceptionFormat     "full"
            showCauses          true
            showStackTraces     true

            afterSuite { desc, result ->
                if (!desc.parent) {
                    def tests = "[${result.resultType}]: (${result.testCount} tests"
                    def passes = "${result.successfulTestCount} successes"
                    def fails = "${result.failedTestCount} failures"
                    def skips = "${result.skippedTestCount} skipped)"
                    def output = "${tests}, ${passes}, ${fails}, ${skips}"
                    def length = output.length()

                    println('\n' + ('-' * length) + '\n' + output + '\n' + ('-' * length))
                }
            }
        }
    }
}

dependencies {
    def libraries = rootProject.ext.libraries

    implementation project(':data')

    compileOnly libraries.javaxAnnotation

    // Annotation Processors
    kapt libraries.daggerCompiler

    // General
    implementation libraries.dagger
    implementation libraries.rxJava
    implementation libraries.rxAndroid
    implementation libraries.stetho
    implementation libraries.stethoOkHttp
    implementation libraries.androidAppCompat
    implementation libraries.androidAnnotations
    implementation libraries.androidRecyclerView
    implementation libraries.androidConstraintLayout
    implementation libraries.androidDesign
    implementation libraries.androidCardView
    implementation libraries.rxLocation
    implementation libraries.circleProgress

    // Google Play
    implementation libraries.googlePlayBase
    implementation libraries.googlePlayMaps
    implementation libraries.googlePlayLocation

    // Kotlin
    implementation libraries.rxBinding
    implementation libraries.rxAppCompat
    implementation libraries.rxDesign
    implementation libraries.extrasDelegates

    // Debug
    implementation libraries.leakCanary
    implementation libraries.timber

    // Testing
    testImplementation libraries.junit
    testImplementation libraries.mockito
    testImplementation libraries.kluent
    testImplementation libraries.jodaTime

    // Android Testing
    androidTestImplementation libraries.mockito
    androidTestImplementation libraries.androidTestRunner
    androidTestImplementation libraries.androidTestRules
    androidTestImplementation libraries.androidEspresso
    androidTestImplementation libraries.androidEspressoIntents
    androidTestImplementation libraries.androidUiAutomator
    androidTestImplementation libraries.androidAnnotations
}
