
buildscript {
    ext.kotlinVersion = '1.1.2-3'

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        jcenter()
    }

    ext {
        androidApplicationId =          "com.mylb.mylogbook.presentation"
        androidVersionCode =            1
        androidVersionName =            "1.0"
        androidBuildToolsVersion =      "25.0.3"
        androidMinSdkVersion =          21
        androidTargetSdkVersion =       25
        androidCompileSdkVersion =      25
        testInstrumentationRunner =     "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId =             "com.mylb.mylogbook.presentation.test"

        libraries = [
            // General
            dagger:                     "com.google.dagger:dagger:2.11-rc2",
            daggerCompiler:             "com.google.dagger:dagger-compiler:2.11-rc2",
            rxJava:                     "io.reactivex.rxjava2:rxjava:2.1.0",
            gson:                       "com.google.code.gson:gson:2.8.0",
            okHttp:                     "com.squareup.okhttp3:okhttp:3.8.0",
            retrofit:                   "com.squareup.retrofit2:retrofit:2.3.0",
            retrofitRxAdapter:          "com.squareup.retrofit2:adapter-rxjava2:2.3.0",
            retrofitGsonConverter:      "com.squareup.retrofit2:converter-gson:2.3.0",
            kotpref:                    "com.chibatching.kotpref:kotpref:2.1.1",

            // Debug
            leakCanary:                 "com.squareup.leakcanary:leakcanary-android:1.5.1",
            timber:                     "com.jakewharton.timber:timber:4.5.1",

            // Testing
            junit:                      "junit:junit:4.12",
            mockito:                    "org.mockito:mockito-core:2.8.9",
            kluent:                     "org.amshove.kluent:kluent:1.21",
            androidTestRunner:          "com.android.support.test:runner:0.5",
            androidTestRules:           "com.android.support.test:rules:0.5",
            androidEspresso:            "com.android.support.test.espresso:espresso-core:2.2.2",
            androidEspressoIntents:     "com.android.support.test.espresso:espresso-intents:2.2.2",
            androidUiAutomator:         "com.android.support.test.uiautomator:uiautomator-v18:2.1.2",

            // Java
            javaxAnnotation:            "javax.annotation:jsr250-api:1.0",
            javaxInject:                "javax.inject:javax.inject:1",

            // Android
            rxAndroid:                  "io.reactivex.rxjava2:rxandroid:2.0.1",
            extrasDelegates:            "me.eugeniomarletti:android-extras-delegates:1.0.5",
            androidAppCompat:           "com.android.support:appcompat-v7:25.3.1",
            androidRecyclerView:        "com.android.support:recyclerview-v7:25.3.1",
            androidAnnotations:         "com.android.support:support-annotations:25.3.1",
            androidConstraintLayout:    "com.android.support.constraint:constraint-layout:1.0.2",
            androidDesign:              "com.android.support:design:25.3.1",

            // Kotlin
            kotlin:                     "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion",
            rxKotlin:                   "io.reactivex.rxjava2:rxkotlin:2.0.3",
            rxBinding:                  "com.jakewharton.rxbinding2:rxbinding-kotlin:2.0.0",
            rxAppCompat:                "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.0.0",
            rxDesign:                   "com.jakewharton.rxbinding2:rxbinding-design-kotlin:2.0.0",
            rxRecyclerView:             "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:2.0.0"
        ]
    }
}

task runDomainUnitTests(dependsOn: [':domain:test']) {
    description 'Run unit tests for the domain layer.'
}

task runDataUnitTests(dependsOn: [':data:cleanTestDebugUnitTest',
                                  ':data:testDebugUnitTest']) {

    description 'Run unit tests for the data layer.'
}

task runPresentationUnitTests(dependsOn: [':presentation:cleanTestDebugUnitTest',
                                          ':presentation:testDebugUnitTest']) {

    description 'Run unit tests for the presentation layer.'
}

task runUnitTests(dependsOn: ['runDomainUnitTests',
                              'runDataUnitTests',
                              'runPresentationUnitTests']) {

    description 'Run all unit tests.'
}

task runAcceptanceTests(dependsOn: [':presentation:connectedAndroidTest']) {
    description 'Run application acceptance tests.'
}